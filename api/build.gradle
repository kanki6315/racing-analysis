buildscript {
    dependencies {
        classpath("org.flywaydb:flyway-database-postgresql:11.9.1")
    }
}
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'nu.studer.jooq' version '10.0'
    id 'org.flywaydb.flyway' version '11.9.1'
}

group = 'com.arjunakankipati'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator:3.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-jooq:3.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.5.0'
    implementation 'org.flywaydb:flyway-core:11.9.1'
    implementation 'org.flywaydb:flyway-database-postgresql:11.9.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.postgresql:postgresql:42.7.7'
    implementation 'com.google.guava:guava:33.1.0-jre'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    jooqGenerator 'org.postgresql:postgresql:42.7.7'
}

tasks.named('test') {
	useJUnitPlatform()
}

jooq {
    version = '3.19.23'
    configurations {
        main {
            generationTool {
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = System.getenv('SPRING_DATASOURCE_URL') ?: 'jdbc:postgresql://localhost:5432/statsdb'
                    user = System.getenv('SPRING_DATASOURCE_USERNAME') ?: 'postgres'
                    password = System.getenv('SPRING_DATASOURCE_PASSWORD') ?: 'stats'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = ".*"
                        excludes = ""
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'com.arjunakankipati.racingstatanalysis.jooq'
                        directory = 'src/main/java/jooq'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}

// Flyway configuration
flyway {
    url = System.getenv('SPRING_DATASOURCE_URL') ?: 'jdbc:postgresql://localhost:5432/statsdb'
    user = System.getenv('SPRING_DATASOURCE_USERNAME') ?: 'postgres'
    password = System.getenv('SPRING_DATASOURCE_PASSWORD') ?: 'stats'
    driver = 'org.postgresql.Driver'
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
}